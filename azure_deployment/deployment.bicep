// SMTP OAuth Relay Azure Deployment
// Deploys: Azure Container Instance, Key Vault, Storage Account with Table

@description('Location for all resources')
param location string = resourceGroup().location

@description('Name of the Azure Container Group')
param containerGroupName string

@description('Container image version to deploy')
param containerVersion string = '1'

@description('Require TLS for SMTP connections')
param requireTls bool = true

@description('SMTP OAuth Relay environment variables')
param envVars object = {}


@description('URL of the Azure Table to use for lookup')
param lookupTableUrl string = ''

@description('Key Vault URL where the TLS certificate is stored')
param tlsCertKeyVaultUrl string = ''

@description('Name of the TLS certificate to use')
param tlsCertName string = ''


var containerImage = 'ghcr.io/justiniven/smtp-oauth-relay:${containerVersion}'
var finalEnvVars = {
  REQUIRE_TLS: string(requireTls)
  ...(requireTls ? { TLS_SOURCE: (tlsCertKeyVaultUrl != '' && tlsCertName != '') ? 'keyvault' : 'file' } : {})
  ...(lookupTableUrl != '' ? { AZURE_TABLES_URL: lookupTableUrl } : {})
  ...(tlsCertKeyVaultUrl != '' ? { AZURE_KEY_VAULT_URL: tlsCertKeyVaultUrl } : {})
  ...(tlsCertName != '' ? { AZURE_KEY_VAULT_CERT_NAME: tlsCertName } : {})
  ...envVars
}


resource containerGroup 'Microsoft.ContainerInstance/containerGroups@2023-05-01' = {
  name: containerGroupName
  location: location
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    containers: [
      {
        name: 'smtp-oauth-relay'
        properties: {
          image: containerImage
          resources: {
            requests: {
              cpu: 1
              memoryInGB: 2
            }
          }
          environmentVariables: [
            for key in objectKeys(finalEnvVars) : {
              name: key
              value: finalEnvVars[key]
            }
          ]
          ports: [
            {
              protocol: 'TCP'
              port: 8025
            }
          ]
        }
      }
    ]
    restartPolicy: 'OnFailure'
    osType: 'Linux'
    ipAddress: {
      type: 'Public'
      autoGeneratedDomainNameLabelScope: 'SubscriptionReuse'
      dnsNameLabel: toLower(containerGroupName)
      ports: [
        {
          protocol: 'TCP'
          port: 8025
        }
      ]
    }
  }
}
